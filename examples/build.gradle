import org.apache.tools.ant.taskdefs.condition.Os

/*
 * Copyright (c) 2021, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * WSO2 Inc. licenses this file to you under the Apache License,
 * Version 2.0 (the "License"); you may not use this file except
 * in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

apply plugin: 'java'

description = 'Ballerina - Persist Examples'

def ballerinaDist = "${project.rootDir}/target/ballerina-runtime"
def examples = ["medical-center"]

clean {
    examples.forEach { example ->
        delete "${projectDir}/${example}/target"
        delete "${projectDir}/${example}/Dependencies.toml"
    }
}

def tomlVersion = stripBallerinaExtensionVersion("${project.version}")

def stripBallerinaExtensionVersion(String extVersion) {
    if (extVersion.matches(project.ext.timestampedVersionRegex)) {
        def splitVersion = extVersion.split('-')
        if (splitVersion.length > 3) {
            def strippedValues = splitVersion[0..-4]
            return strippedValues.join('-')
        } else {
            return extVersion
        }
    } else {
        return extVersion.replace("${project.ext.snapshotVersion}", '')
    }
}

task updateTomlFiles {
    examples.each { example ->
        def ballerinaTomlFilePlaceHolder = new File("${project.rootDir}/examples/build-config/${example}/Ballerina.toml")
        def ballerinaTomlFile = new File("${project.rootDir}/examples/${example}/Ballerina.toml")

        def newConfig = ballerinaTomlFilePlaceHolder.text.replace('@project.version@', project.version.toString())
        newConfig = newConfig.replace('@toml.version@', tomlVersion)
        ballerinaTomlFile.text = newConfig
    }
}

task commitTomlFiles {
    doLast {
        project.exec {
            ignoreExitValue true
            workingDir "${project.rootDir}/examples"
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine 'cmd', '/c', "git commit -m \"[Automated] Update native jar versions in exmaple toml files\" */Ballerina.toml"
            } else {
                commandLine 'sh', '-c', "git commit -m \"[Automated] Update native jar versions in exmaple toml files\" */Ballerina.toml"
            }
        }
    }
}

task testExamples {
    doLast {
        examples.each { example ->
            try {
                exec {
                    workingDir "${project.projectDir}/${example}"
                    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                        commandLine 'cmd', '/c', "${ballerinaDist}/bin/bal.bat test --offline && exit %%ERRORLEVEL%%"
                    } else {
                        commandLine 'sh', '-c', "${ballerinaDist}/bin/bal test --offline"
                    }
                }
            } catch (Exception e) {
                println("Example '${example}' Build failed: " + e.message)
                throw e
            }
        }
    }
}

task buildExamples {
    gradle.taskGraph.whenReady { graph ->
        if (graph.hasTask(":persist-examples:testExamples")) {
            buildExamples.enabled = false
        }
    }
    doLast {
        examples.each { example ->
            try {
                exec {
                    workingDir "${project.projectDir}/${example}"
                    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                        commandLine 'cmd', '/c', "${ballerinaDist}/bin/bal.bat build --offline && exit %%ERRORLEVEL%%"
                    } else {
                        commandLine 'sh', '-c', "${ballerinaDist}/bin/bal build --offline"
                    }
                }
            } catch (Exception e) {
                println("Example '${example}' Build failed: " + e.message)
                throw e
            }
        }
    }
}

buildExamples.dependsOn ":persist-ballerina:build"
testExamples.dependsOn ":persist-ballerina:build"

buildExamples.dependsOn updateTomlFiles
testExamples.dependsOn updateTomlFiles
updateTomlFiles.finalizedBy commitTomlFiles

build.dependsOn buildExamples
test.dependsOn testExamples
